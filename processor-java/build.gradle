plugins {
  id 'org.jetbrains.kotlin.jvm' version '1.3.31'
  id 'org.jetbrains.dokka' version '0.9.18'
  id 'maven-publish'
  id 'java-library'
  id 'com.jfrog.bintray' version '1.8.4'
}

repositories {
  jcenter()
  google()
}
dependencies {
  implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.3.31'
  implementation project(':annotations')
  implementation project(':processor-base')
  implementation 'com.squareup:javapoet:1.11.1'
}

task sourcesJar(type: Jar) {
  group = JavaBasePlugin.DOCUMENTATION_GROUP
  description = 'Assembles a sources JAR'
  archiveClassifier = 'sources'
  from sourceSets.main.allSource
}

task kdocJar(type: Jar) {
  group = JavaBasePlugin.DOCUMENTATION_GROUP
  description = 'Assembles a kdoc JAR'
  archiveClassifier = 'javadoc'
  from dokka
}

publishing {
  publications {
    maven(MavenPublication) {
      artifactId project.name
      artifact sourcesJar
      artifact kdocJar
      groupId 'com.github.stoyicker.test-accessors'
      pom {
        name = project.name
        description = project.name
        url = 'https://github.com/stoyicker/test-accessors'
        licenses {
          license {
            name = 'Creative Commons Attribution 4.0 International'
            url = 'https://creativecommons.org/licenses/by/4.0/legalcode'
          }
        }
        developers {
          developer {
            id = 'stoyicker'
            name = 'Jorge Antonio Diaz-Benito Soriano'
            email = 'jorge.diazbenitosoriano@gmail.com'
          }
        }
        scm {
          connection = 'scm:git:https://github.com:stoyicker/test-accessors.git'
          developerConnection = 'scm:git:ssh:git@github.com:stoyicker/test-accessors.git'
          url = 'https://github.com/stoyicker/test-accessors'
        }
        version = {
          def stdout = new ByteArrayOutputStream()
          exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = stdout
          }
          return stdout.toString().replaceAll("[^\\d|.]", "")
        }()
      }
      from components.java
    }
  }
}

bintray {
  user = "$System.env.BINTRAY_USER"
  key = "$System.env.BINTRAY_API_KEY"
  publications = ['maven']
  pkg {
    repo = 'test-accessors'
    name = project.name
    userOrg = 'stoyicker-org'
    vcsUrl = 'ssh:git@github.com:stoyicker/test-accessors.git'
    licenses = ['Creative Commons Attribution 4.0 International']
    issueTrackerUrl = 'https://github.com/stoyicker/test-accessors/issues'
  }
  version {
    name = {
      def stdout = new ByteArrayOutputStream()
      exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
      }
      return stdout.toString().replaceAll("[^\\d|.]", "")
    }()
    gpg {
      sign = true
    }
    mavenCentralSync {
      user = "$System.env.OSS_USER"
      password = "$System.env.OSS_PASSWORD"
    }
  }
}

buildscript {
  repositories {
    google()
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:3.3.2'
    classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.21'
  }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
  buildToolsVersion '28.0.3'
  compileSdkVersion 28
  signingConfigs {
    dummy1 {
      storeFile new File('dummy1.keystore')
      storePassword('dummy1')
      keyAlias('dummy1')
      keyPassword('dummy1')
    }
  }
  buildTypes {
    debug {
      minifyEnabled false
    }
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard/rules.pro'
      signingConfig signingConfigs.dummy1
    }
  }
  defaultConfig {
    applicationId 'com.github.stoyicker.testaccessors.sampleandroid'
    versionCode 1
    versionName '1'
    minSdkVersion 1
    targetSdkVersion 28
  }
  flavorDimensions 'language'
  productFlavors {
    java {
      dimension 'language'
      applicationIdSuffix '.java'
      versionNameSuffix '-java'
    }
    kotlin {
      dimension 'language'
      applicationIdSuffix '.kotlin'
      versionNameSuffix '-kotlin'
    }
  }
}

tasks.findByName('lint').enabled = false

repositories {
  google()
  jcenter()
}
dependencies {
  javaCompileOnly project(':annotations')
  // Kapt takes over annotationProcessor everywhere, so javaAnnotationProcessor cannot be used here
  kaptJava project(':processor-java')
  testJavaImplementation 'junit:junit:4.12'

  kotlinImplementation 'org.jetbrains.kotlin:kotlin-stdlib:1.3.21'
  kotlinCompileOnly project(':annotations')
  kaptKotlin project(':processor-kotlin')
  testKotlinImplementation 'junit:junit:4.12'
}

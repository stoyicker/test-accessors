// This file requires maven-publish, java-library, signing and com.jfrog.bintray

project.version = {
  def stdout = new ByteArrayOutputStream()
  exec {
    commandLine 'git', 'describe', '--abbrev=0'
    standardOutput = stdout
  }
  return stdout.toString().replaceAll("[^\\d|.]", "")
}()

jar {
  archiveFileName = "${project.name}.jar"
}

task sourcesJar(type: Jar) {
  group = JavaBasePlugin.DOCUMENTATION_GROUP
  description = 'Assembles a sources JAR'
  archiveClassifier.set('sources')
  archiveFileName = "${project.name}-sources.jar"
  from sourceSets.main.allSource
}

task kdocJar(type: Jar) {
  group = JavaBasePlugin.DOCUMENTATION_GROUP
  description = 'Assembles a kdoc JAR'
  archiveClassifier.set('javadoc')
  archiveFileName = "${project.name}-javadoc.jar"
  from dokka
}

// No per-release signing, Bintray does the signing before syncing to Central
//signing {
//  sign jar
//  sign sourcesJar
//  sign kdocJar
//}
//ext."signing.keyId" = "$System.env.GPG_KEY_ID"
//ext."signing.password" ="$System.env.GPG_KEY_PASSWORD"
//ext."signing.secretKeyRingFile" = "$System.env.GPG_SECRET_KEY_FILE"
//
//task signArtifacts {
//  afterEvaluate {
//    dependsOn signJar, signSourcesJar, signKdocJar, generatePomFileForMavenPublication
//  }
//  doLast {
//    signing.sign file("${buildDir}/publications/maven/pom-default.xml")
//  }
//}

publishing {
  publications {
    maven(MavenPublication) {
      artifactId project.name
      version project.version
      artifact sourcesJar
      artifact kdocJar
      groupId 'com.github.stoyicker.test-accessors'
      pom {
        name = project.name
        description = project.name
        url = 'https://github.com/stoyicker/test-accessors'
        licenses {
          license {
            name = 'Apache-2.0'
            url = 'https://www.apache.org/licenses/LICENSE-2.0'
          }
        }
        developers {
          developer {
            id = 'stoyicker'
            name = 'Jorge Antonio Diaz-Benito Soriano'
            email = 'jorge.diazbenitosoriano@gmail.com'
          }
        }
        scm {
          connection = 'scm:git:https://github.com:stoyicker/test-accessors.git'
          developerConnection = 'scm:git:ssh:git@github.com:stoyicker/test-accessors.git'
          url = 'https://github.com/stoyicker/test-accessors'
        }
      }
      from components.java
    }
  }
  repositories {
    maven {
      url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
      credentials {
        username = "$System.env.OSS_USER"
        password = "$System.env.OSS_PASSWORD"
      }
    }
  }
}

bintray {
  user = "$System.env.BINTRAY_USER"
  key = "$System.env.BINTRAY_API_KEY"
  publications = ['maven']
  publish = true
  pkg {
    repo = 'test-accessors'
    name = project.name
    vcsUrl = 'ssh:git@github.com:stoyicker/test-accessors.git'
    licenses = ['Apache-2.0']
    desc = 'An annotation processor that generates code for your tests to be able to access and modify private/final fields so you don\'t have to use approaches such as @VisibleForTesting!'
    publicDownloadNumbers = true
    githubRepo = 'stoyicker/test-accessors'
    websiteUrl = 'https://github.com/stoyicker/test-accessors/'
    issueTrackerUrl = 'https://github.com/stoyicker/test-accessors/issues'
    version {
      name = project.version
      vcsTag = "v${project.version}"
      released = new Date()
      gpg {
        sign = true
      }
      mavenCentralSync {
        user = "$System.env.OSS_USER"
        password = "$System.env.OSS_PASSWORD"
      }
    }
  }
}
